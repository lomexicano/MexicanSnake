$${
#slotclick_delay = 0;
#movement_automatic_delay = 12;

log(" ");
toggle(@mexican_snake);
if(!@mexican_snake);
	arraysize(#slots_snake[],#score);
	if(#score > 0);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fYour score was: &a%#score%&f!");
		unset(#slots_snake[]);
	endif;
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &c&lOFF&4!");
	stop("MEXICAN_SNAKE");
	stop;
endif;
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &a&lON&2!");

if(GUI != "GUICHEST");
	if((HITID != 54)&&(HITID != 146)&&(HITID != "chest")&&(HITID != "trapped_chest"));
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cLook at a &4double chest &cbefore starting!");
		unset(@mexican_snake);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &c&lOFF&4!");
		stop("MEXICAN_SNAKE");
		stop;
	else;
		key(use);
		do(20);
			wait(2t);
		until(GUI == "GUICHEST");
		wait(1t);
	endif;
endif;

if(GUI != "GUICHEST");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cThe chest was not opened correctly!");
	unset(@mexican_snake);
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &c&lOFF&4!");
	stop("MEXICAN_SNAKE");
	stop;
endif;
ifmatches(%CONTAINERSLOTS%,"\d");
	if(CONTAINERSLOTS < 90);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cYou must open a double sized chest &8(54 slots)&c!");
		unset(@mexican_snake);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &c&lOFF&4!");
		stop("MEXICAN_SNAKE");
		stop;
	endif;
endif;

ifmatches("%ITEM%","[a-z]");
	set(&id_body,"wool:5");
	set(&id_head,"wool:14");
	set(&id_apple,"apple:0");
else;
	set(&id_body,"35:5");
	set(&id_head,"35:14");
	set(&id_apple,"260:0");
endif;

unsafe(500);
	for(#slot,0,53);
		ifmatches("%ITEM%","[a-z]");
			getslotitem(%#slot%,&id,#stack);
		else;
			getslotitem(%#slot%,#id,#stack);
		endif;
		if(#stack > 0);
			slotclick(%#slot%);
			if(#slotclick_delay > 0);
				wait(%#slotclick_delay%t);
			endif;
			slotclick(-999);
			if(#slotclick_delay > 0);
				wait(%#slotclick_delay%t);
			endif;
		endif;
	next;

	unset(#amount_blocks_head);
	unset(#amount_blocks_body);
	unset(#amount_blocks_apple);

	for(#slot,54,89);
		ifmatches("%ITEM%","[a-z]");
			getslotitem(%#slot%,&id,#stack,#data);
		else;
			getslotitem(%#slot%,#id,#stack,#data);
			set(&id,"%#id%");
		endif;
		ifmatches("%&id%:%#data%","^(%&id_head%)$");
			inc(#amount_blocks_head,%#stack%);
		endif;
		ifmatches("%&id%:%#data%","^(%&id_body%)$");
			inc(#amount_blocks_body,%#stack%);
		endif;
		ifmatches("%&id%:%#data%","^(%&id_apple%)$");
			inc(#amount_blocks_apple,%#stack%);
		endif;
	next;
endunsafe;

match("%&id_apple%","^([^:]+):(\d+)$",{&id_apple_itself,#id_apple_data});
match("%&id_body%","^([^:]+):(\d+)$",{&id_body_itself,#id_body_data});
match("%&id_head%","^([^:]+):(\d+)$",{&id_head_itself,#id_head_data});

if(#amount_blocks_apple < 1);
	echo("/give %PLAYER% %&id_apple_itself% 1 %#id_apple_data%");
	wait(2t);
endif;
if(#amount_blocks_body < 52);
	echo("/give %PLAYER% %&id_body_itself% 64 %#id_body_data%");
	wait(2t);
endif;
if(#amount_blocks_head < 1);
	echo("/give %PLAYER% %&id_head_itself% 1 %#id_head_data%");
	wait(2t);
endif;

getslot(%&id_apple%,#slotclick,54);
slotclick(%#slotclick%);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;
random(#slot_apple,9,53);
slotclick(%#slot_apple%,r);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;
slotclick(%#slotclick%);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;

getslot(%&id_head%,#slotclick,54);
slotclick(%#slotclick%);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;
slotclick(0,r);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;
slotclick(%#slotclick%);
if(#slotclick_delay > 0);
	wait(%#slotclick_delay%t);
endif;

set(&moving,"right");

unset(#slots_snake[]);
push(#slots_snake[],0);

do();
	do(%#movement_automatic_delay%);
		if((KEY_LEFT)||(KEY_A));
			#read = #slots_snake[0] - 1;
		elseif((KEY_RIGHT)||(KEY_D));
			#read = #slots_snake[0] + 1;
		elseif((KEY_UP)||(KEY_W));
			#read = #slots_snake[0] - 9;
		elseif((KEY_DOWN)||(KEY_S));
			#read = #slots_snake[0] + 9;
		endif;
		if((#read != #slots_snake[1])&&(!KEY_SPACE)&&(!KEY_RETURN));
			if((KEY_LEFT)||(KEY_A));
				set(&moving,"left");
			elseif((KEY_RIGHT)||(KEY_D));
				set(&moving,"right");
			elseif((KEY_UP)||(KEY_W));
				set(&moving,"up");
			elseif((KEY_DOWN)||(KEY_S));
				set(&moving,"down");
			endif;
		endif;
	until((KEY_SPACE)||(KEY_RETURN)||(GUI != "GUICHEST"));
	if((KEY_SPACE)||(KEY_RETURN));
		ifmatches("%ITEM%","[a-z]");
			achievementget("§fGame paused!","wool:0");
		else;
			achievementget("§fGame paused!","35:0");
		endif;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &eGame Paused!");
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &ePress &6SPACE &eagain to &6resume &7(with the chest opened)");
		do();
		until((!KEY_SPACE)&&(!KEY_RETURN));
		do();
		until((KEY_SPACE)||(KEY_RETURN));
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &aGame Resumed!");
		ifmatches("%ITEM%","[a-z]");
			achievementget("§aGame resumed!","wool:5");
		else;
			achievementget("§aGame resumed!","35:5");
		endif;
	elseif(GUI != "GUICHEST");
		break;
	else;
		//log("Movement: %&moving%");
		unsafe(0);
			unset(you_lose);
			unset(lunch_time);
			arraysize(#slots_snake[],#size);
			dec(#size);
			for(#index,0,%#size%);
				set(#pos,%#slots_snake[%#index%]%);
				#slotclick = #pos;
				if(#index == 0);
					#next_body_block_pos = #pos;
					if(&moving == "right");
						inc(#pos,1);
						if((#pos == 9)||(#pos == 18)||(#pos == 27)||(#pos == 36)||(#pos == 45)||(#pos == 54));
							set(you_lose);
							break;
						endif;
					elseif(&moving == "left");
						dec(#pos,1);
						if((#pos == -1)||(#pos == 8)||(#pos == 17)||(#pos == 26)||(#pos == 35)||(#pos == 44));
							set(you_lose);
							break;
						endif;
					elseif(&moving == "up");
						dec(#pos,9);
						if(#pos < 0);
							set(you_lose);
							break;
						endif;
					else;
						inc(#pos,9);
						if(#pos > 53);
							set(you_lose);
							break;
						endif;
					endif;
					ifmatches("%ITEM%","[a-z]");
						getslotitem(%#pos%,&id,#stack,#data);
						set(&id,"%&id%:%#data%");
					else;
						getslotitem(%#pos%,#id,#stack,#data);
						set(&id,"%#id%:%#data%");
					endif;
					//log("%#slotclick% -> %#pos%");
					if(&id == &id_apple);
						set(lunch_time);
						slotclick(%#pos%,l,true);
						if(#slotclick_delay > 0);
							wait(%#slotclick_delay%t);
						endif;
					elseif(#stack > 0);
						set(you_lose);
						break;
					endif;
					slotclick(%#slotclick%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					slotclick(%#pos%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					set(#slots_snake[%#index%],%#pos%);
				else;
					//log("%#slotclick% -> %#next_body_block_pos%");
					slotclick(%#slotclick%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					slotclick(%#next_body_block_pos%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					set(#slots_snake[%#index%],%#next_body_block_pos%);
					#next_body_block_pos = #pos;
				endif;
			next;
			if(lunch_time);
				getslot(%&id_body%,#slotclick,54);
				if(#slotclick < 0);
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cError ;-;");
					gui();
					break;
				else;
					slotclick(%#slotclick%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					slotclick(%#next_body_block_pos%,r);
					push(#slots_snake[],%#next_body_block_pos%);
					arraysize(#slots_snake[],#score);
					dec(#score,1);
					achievementget("§a+1 §8(§7total: §e%#score%§8)",%&id_apple%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
					slotclick(%#slotclick%);
					if(#slotclick_delay > 0);
						wait(%#slotclick_delay%t);
					endif;
				endif;
				ifmatches("%ITEM%","[a-z]");
					getslot("air",#empty);
				else;
					getslot(0,#empty);
				endif;
				if(#empty < 0);
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &aCongratulations, you win!");
					break;
				endif;
				do();
					random(#i,0,53);
					indexof(#slots_snake[],#pos,%#i%);
					//log("random apple: %#i% -> found: %#pos%");
					if(#pos < 0);
						getslot(%&id_apple%,#slotclick,54);
						slotclick(%#slotclick%);
						if(#slotclick_delay > 0);
							wait(%#slotclick_delay%t);
						endif;
						slotclick(%#i%,r);
						if(#slotclick_delay > 0);
							wait(%#slotclick_delay%t);
						endif;
						slotclick(%#slotclick%);
						if(#slotclick_delay > 0);
							wait(%#slotclick_delay%t);
						endif;
						break;
					endif;
					wait(1t);
				loop;
			endif;
		endunsafe;
	endif;
	//log("next");
until((GUI != "GUICHEST")||(you_lose));

if(you_lose);
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cYou lose!");
	playsound("entity.item.break");
	playsound("random.break"); 
	achievementget("§4Game Over!","%&id_head%");
endif;

arraysize(#slots_snake[],#score);
dec(#score,1);
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &eThe game has ended!");
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fYour score was: &a%#score%&f!");
unset(#slots_snake[]);
unset(@mexican_snake);
log(" ");
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &e&lMEXICAN &a&lSNAKE&f: &c&lOFF&4!");
stop("MEXICAN_SNAKE");
}$$
